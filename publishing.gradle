
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "svt open source"
            organisation "SVT"
            organisationUrl "https://www.svt.se/opensource/"
            email "opensource@svt.se"
        }
    }
    
    scm {
       url "https://github.com/SVT/junit5-redis-extension/"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//need for jcenter/maven repo compliance
artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        publishjob(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'se.svt.oss.junit5'
            artifactId 'junit5-redis-extension'
            version  scmVersion.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A JUnit5 extension test for embedded Redis')
                root.appendNode('name', 'junit5-redis-extension')
                root.appendNode('url', 'https://github.com/SVT/junit5-redis-extension')
                root.children().last() + pomConfig
            }
        }
    }
}
